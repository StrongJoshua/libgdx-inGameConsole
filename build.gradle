apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'com.strongjoshua'
version = '0.7.1'

description = """LibGdx In-Game Console"""

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
     maven { url "http://repo.maven.apache.org/maven2" }
}

dependencies {
    compile group: 'com.badlogicgames.gdx', name: 'gdx', version:'1.9.6'
    testCompile group: 'com.badlogicgames.gdx', name: 'gdx-backend-lwjgl', version:'1.9.6'
    testCompile group: 'com.badlogicgames.gdx', name: 'gdx-platform', version:'1.9.6', classifier:'natives-desktop'
    testCompile group: 'com.badlogicgames.gdx', name: 'gdx-box2d', version:'1.9.6'
    testCompile group: 'com.badlogicgames.gdx', name: 'gdx-box2d-platform', version:'1.9.6', classifier:'natives-desktop'
    testCompile group: 'junit', name: 'junit', version:'4.12'
}

sourceSets {
    main {
        java {
            srcDirs = ['src']
        }
        resources {
            srcDirs = ['src']
        }
    }
    test {
        java {
            srcDirs = ['test']
        }
        resources {
            srcDirs = ['test']
        }
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'libGdx In-Game Console'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'libGdx In-Game Console'
                url 'https://www.strongjoshua.net/projects/games/libgdx-ingame-console'

                scm {
                    connection 'scm:git:git@github.com:StrongJoshua/libgdx-inGameConsole.git'
                    developerConnection 'scm:git:git@github.com:StrongJoshua/libgdx-inGameConsole.git'
                    url 'https://github.com/StrongJoshua/libgdx-inGameConsole'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'StrongJoshua'
                        name 'Jan Risse'
                        email 'strongjoshua@hotmail.com'
                    }
                }
            }
        }
    }
}
